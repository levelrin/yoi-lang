plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.9'
    id 'pmd'
    id 'checkstyle'
    id 'antlr'
}

group 'com.levelrin'
version '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            // We will generate the parser in this directory using Antlr.
            // We need to add this directory to the main source set to use the generated classes in our code.
            srcDirs("$buildDir/generated-src/antlr/main")
        }
    }
}

dependencies {
    antlr 'org.antlr:antlr4:4.11.1'
    implementation 'net.jcip:jcip-annotations:1.0'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    implementation 'org.jetbrains:annotations:23.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:4.8.0'
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                enabled = true
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'COMPLEXITY'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
            limit {
                enabled = true
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.00000
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestCoverageVerification
check.dependsOn jacocoTestReport

spotbugs {
    toolVersion = '4.7.1'
    ignoreFailures = false
    effort = 'max'
    excludeFilter = file('config/spotbugs/exclude.xml')
}
spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

pmd {
    toolVersion = '6.46.0'
    ignoreFailures = false
    ruleSetFiles = files('config/pmd/ruleSet.xml')
    ruleSets = []
}
tasks.withType(Pmd) {
    reports {
        xml.required = true
        html.required = true
    }
}

checkstyle {
    // 9.3 is the latest version that supports Java 8.
    toolVersion '9.3'
    ignoreFailures = false
    configFile = file('config/checkstyle/checkstyle.xml')
    configProperties = [
        'checkstyle.header.file': file('config/checkstyle/java.header'),
        'checkstyle.suppression.file': file('config/checkstyle/suppression.xml')
    ]
}
tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.levelrin.yoilang.Main'
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

generateGrammarSource {
    maxHeapSize = '64m'
    arguments += [
        '-visitor',
        '-long-messages',
        // This will add the package declaration in all generated classes.
        // Package declaration is required to use the generated classes in our code
        // because Java doesn't allow importing the classes in the default package.
        '-package', 'com.levelrin.antlr.generated'
    ]
    // Since we will add the package declaration, we must put generated classes in the package folder.
    outputDirectory = file("$buildDir/generated-src/antlr/main/com/levelin/antlr/generated")
}
